:grammar:
    <програма>::=<спільний блок змінних> { <блок операторів> }|<програма>\n



<:Блок ініціалізації змінних:>
    <спільний блок змінних>::=<блок змінних>


    <блок змінних>::=<ініціалізація змінних>\n<блок змінних>|<ініціалізація змінних>\n
    <ініціалізація змінних>::=<тип><список змінних>
    <тип>::=int|double|bool
    <список змінних>::=<ініціалізація змінної>,<список змінних>|<ініціалізація змінної>
    <ініціалізація змінної>::= <змінна> = <арифметичний вираз ініціалізації> | IDN | <змінна> = <відношення ініціалізації>
    <змінна>::=IDN
    <арифметичний вираз ініціалізації>::=<спільний арифметичний вираз>
    <відношення ініціалізації>::= <спільне відношення>

<;Блок ініціалізації змінних;>

<:Блок операторів:>
    <блок операторів>::=<оператор>\n|<оператор>\n<блок операторів>
    <оператор>::=<цикл>|<умовний перехід>|<оператор вводу>|<оператор виводу>|<оператор присвоєння>|<мітка>
    <цикл>::= for <змінна1> = <операторний арифметичний вираз> to <спільний арифметичний вираз> by <спільний арифметичний вираз> while (<спільне відношення>) {<блок операторів>}
    <умовний перехід>::=if <спільне відношення> then {<блок операторів>}|if <спільне відношення> then {<блок операторів>} else {<блок операторів>}
    <оператор вводу>::= cin <аргументи оператора вводу>
    <аргументи оператора вводу>::= << IDN| << IDN <аргументи оператора вводу>
    <вираз>::=<спільний арифметичний вираз>|<спільне відношення>|IDN
    <оператор виводу>::= cout <аргументи оператора виводу>
    <аргументи оператора виводу>::= >> <вираз>|>> <вираз> <аргументи оператора виводу>
    <оператор присвоєння>::= <змінна1> = <вираз>
    <змінна1>::=IDN
    <мітка>::=POINTER|POINTER :

    <операторний арифметичний вираз>::=<спільний арифметичний вираз>
<;Блок операторів;>

<:Відношення:>
    <спільне відношення>::=<відношення>


    <відношення>::=<лівий арифметичний вираз><знак відношення><правий арифметичний вираз>
    <знак відношення>::=>|<|>=|<=|!=|==

    <правий арифметичний вираз>::=<лівий арифметичний вираз>
    <лівий арифметичний вираз>::=<спільний арифметичний вираз>

<;Відношення;>

<:Арифметичний вираз:>
    <спільний арифметичний вираз>::=<арифметичний вираз>


    <арифметичний вираз>::=<арифметичний добуток>|<арифметичний вираз>+<арифметичний добуток>|<арифметичний вираз>-<арифметичний добуток>

    <арифметичний добуток>::=<добуток>
    <добуток>::=<арифметичний вираз у дужках>|<добуток>*<арифметичний вираз у дужках>|<добуток>/<арифметичний вираз у дужках>

    <арифметичний вираз у дужках>::=<вираз у дужках>
    <вираз у дужках>::=(<циклічний арифметичний вираз>)|CON|IDN

    <циклічний арифметичний вираз>::=<арифметичний вираз>

<;Арифметичний вираз;>

:types:
    IDN:;=int|double|bool

:regex:
    IDN;;=^[a-zA-Z_][a-zA-Z0-9_]*$
    CON;;=^([1-9][0-9]*|0)?\.[0-9]+$|^[1-9][0-9]*$|^0$
    POINTER;;=^#[a-zA-Z0-9_]+$
